#!/usr/bin/perl

use strict;
use warnings;

use Net::Proxmox::VE;
use Data::Dumper;
use Getopt::Long;

use IO::Socket::SSL qw(SSL_VERIFY_NONE);

my $host;
my $realm;
my $username;
my $password;
my $cluster;
my $debug;

sub usage {
    print "usage: $0 PARAMS\n";
    print "with PARAMS:\n";
    print "\t--host HOST\n";
    print "\t--realm REALM (your realm: pam/pve/ldap)\n";
    print "\t--username USERNAME\n";
    print "\t--password PASSWORD\n";
    print "\t--cluster CLUSTER (netbox cluster name)\n";
    print "\t--debug (optional)\n";

    exit 1;
}

GetOptions(
    'host=s'     => \$host,
    'realm=s'    => \$realm,
    'username=s' => \$username,
    'password=s' => \$password,
    'cluster=s'  => \$cluster,
    'debug'      => \$debug,
) or usage();

if ( !$host or !$realm or !$username or !$password or !$cluster ) {
    usage();
}

my $pve = Net::Proxmox::VE->new(
    host     => $host,
    realm    => $realm,
    username => $username,
    password => $password,
    debug    => $debug,
    ssl_opts => {
        SSL_verify_mode => SSL_VERIFY_NONE,
        verify_hostname => 0
    },
);

die "login failed\n"          unless $pve->login;
die "invalid login ticket\n"  unless $pve->check_login_ticket;
die "unsupport api version\n" unless $pve->api_version_check;

my $resources = $pve->get("/cluster/resources");

# openvz and qemu objects are exactly the same.
# only the item->{type} value differs (openvz or qemu)
print "name,cluster,role,vcpus,memory,disk,comments\n";
foreach my $item (@$resources) {
    next unless $item->{type} eq 'qemu';

    my $ram  = $item->{maxmem} / 1024 / 1024;
    my $disk = $item->{maxdisk} / 1024 / 1024 / 1024;

    print "$item->{name},";
    print "$cluster,";
    print "Virtual Machine,";
    print "$item->{maxcpu},";
    print "$ram,";
    print "$disk,";
    print "vmid=$item->{vmid}";
    print "\n";
}
